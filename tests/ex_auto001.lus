/* From ColaÃ§o, Pagano, Pouzet 2005 */

node chrono (StSt: bool; Rst: bool) = (disp_1: int; disp_2: int) 
with var shared m, s: int; shared run: bool  in
  automaton
    state CHRONO 
      do automaton
        state STOP 
          do s = 0 -> last s;
            m = 0 -> last m;
            run = false
            unless StSt continue START
        state START
          with var d: int in
          do 
            d = 0 -> (pre d + 1) mod 100;
            s = 0 -> if d < pre d
                then (last s + 1) mod 60
                else last s;
            m = if s < last s
                then (last m + 1) mod 60
                else last m;
            run = true
            unless StSt continue STOP
       end
       until Rst and not run then CHRONO
  end; 
  automaton
    state TIME
      do disp_1 = s;
        disp_2 = m
        until Rst and run then LAP
    state LAP
      do until Rst then TIME
  end
